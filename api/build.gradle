apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'osgi'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

archivesBaseName = "api"

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile "org.slf4j:slf4j-api:$slf4j_version"

    testCompile "junit:junit:$junit_version"
    testCompile "com.google.truth:truth:$truth_version"
    testCompile "org.mockito:mockito-core:$mockito_core_version"
    testCompile "com.nhaarman:mockito-kotlin:$mockito_kotlin_version"
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Dimwit Light Controller API'
        packaging 'jar'
        groupId = release_groupId
        version = release_version
        artifactId 'api'
        description 'An API that tells you what light levels should be based on time of day and configured levels.'
        url 'https://github.com/holmes/dimwit'

        scm {
          connection 'scm:git:git://github.com/holmes/dimwit.git'
          developerConnection 'scm:git:ssh://github.com:holmes/dimwit.git'
          url 'https://github.com/holmes/dimwit/tree/master'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'holmes'
            name 'Jason Holmes'
            email 'holmes.j@gmail.com'
          }
        }
      }
    }
  }
}
